/*
 * Copyright (c) 2013 - 2014, Allen A. George <allen dot george at gmail dot com>
 *
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *     * Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *     * Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *     * Neither the name of libraft nor the
 *       names of its contributors may be used to endorse or promote products
 *       derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR
 * ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
 * (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
 * LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
 * ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

description 'Sample consistent, distributed key-value store that uses libraft-agent and libraft-core'

dependencies {
    compile 'com.yammer.dropwizard:dropwizard-core:0.6.2'
    compile project(':libraft-agent')

    testCompile 'com.yammer.dropwizard:dropwizard-testing:0.6.2'
    testCompile 'com.yammer.dropwizard:dropwizard-client:0.6.2'

    runtime 'org.xerial:sqlite-jdbc:3.7.2'
}

javadoc {
    def libraftCoreDocsDir = project(':libraft-core').docsDir
    options.linksOffline "${libraftJavadocUrl}/libraft-core/${canonicalVersion}", "${libraftCoreDocsDir}/javadoc"

    def libraftAgentDocsDir = project(':libraft-agent').docsDir
    options.linksOffline "${libraftJavadocUrl}/libraft-agent/${canonicalVersion}", "${libraftAgentDocsDir}/javadoc"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'io.libraft.kayvee.KayVee',
                'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

task dist(type: Zip, description: "Assembles a zipfile containing everything required to run a KayVee instance", dependsOn: jar) {
    classifier 'dist'
    from configurations.runtime
    from jar
    from 'src/dist'
    into "${project.name}-${version}"
}

artifacts {
    archives dist
}
